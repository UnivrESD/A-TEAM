grammar proposition;

file : boolean EOF;

boolean
    : LPAREN boolean RPAREN
    | PastOp LPAREN boolean ',' constant RPAREN
    | NextOp LPAREN boolean ',' constant RPAREN
    | NOT boolean
    | numeric relop numeric
    | logic relop logic
    | boolean EQ boolean
    | numeric EQ numeric
    | logic EQ logic
    | boolean NEQ boolean
    | numeric NEQ numeric
    | logic NEQ logic
    | boolean booleanop=AND boolean
    | boolean booleanop=OR boolean
    | booleanAtom
    | logic /* implicit cast from logic to boolean */
    | numeric /* implicit cast from numeric to boolean */
    ;

booleanAtom
    : booleanConstant
    | booleanVariable
    ;

booleanConstant
    : BOOLEAN
    ;

booleanVariable
    : '<' variable ',bool>' 
    ;

logic
    : LPAREN logic RPAREN
    | LGPAREN logic RGPAREN LCPAREN constant RCPAREN
    | LGPAREN logic RGPAREN LCPAREN upper_bound ':' lower_bound RCPAREN
    | PastOp LPAREN logic ',' constant RPAREN
    | NextOp LPAREN logic ',' constant RPAREN
    | NEG logic 
    | logic artop=(TIMES|DIV) logic
    | logic artop=(PLUS|MINUS) logic
    | logic logop=BAND logic
    | logic logop=BOR logic
    | logicAtom
    | numeric /* implicit cast from numeric to logic */
    ;

logicAtom
    : logicConstant
    | logicVariable
    ;

logicConstant
    : BINARY
    ;

logicVariable
    : '<' variable ',logic>' 
    ;

numeric
    : LPAREN numeric RPAREN
    | NextOp LPAREN numeric ',' constant RPAREN
    | PastOp LPAREN numeric ',' constant RPAREN
    | numeric artop=(TIMES|DIV) numeric
    | numeric artop=(PLUS|MINUS) numeric
    | numericAtom
    ;

numericAtom
    : numericConstant
    | numericVariable
    ;

numericConstant
    :  NUMERIC
    ;

numericVariable
    : '<' variable ',numeric>' 
    ;

constant
    :  BINARY
    |  NUMERIC
    |  BOOLEAN 
    ;

variable
    : VARIABLE
    ;


NextOp
    : '$next'
    ;
PastOp
    : '$past'
    ;

LGPAREN
    : '{'
    ;

RGPAREN
    : '}'
    ;
LCPAREN
    : '['
    ;

RCPAREN
    : ']'
    ;

LPAREN
    : '('
    ;

RPAREN
    : ')'
    ;

upper_bound:
    constant
    ;

lower_bound:
    constant
    ;

//==== Token VARIABLE ==========================================================
VARIABLE
   : VALID_ID_START VALID_ID_CHAR*
   ;


fragment VALID_ID_START
    : ('a' .. 'z')
    | ('A' .. 'W')
    | ('Y' .. 'Z')
    | ('.')
    | '_'
    ;

fragment VALID_ID_CHAR
    : VALID_ID_START
    | ('0' .. '9')
    | ('.')
    | ('[') ('0' .. '9')+ (':') ('0' .. '9')+ (']')
    | ('[') ('0' .. '9')+ (']')
    ;
//------------------------------------------------------------------------------


//==== Token constant ==========================================================
    NUMERIC
    : ('0' .. '9') + ('.' ('0' .. '9') +)?
    ;
    BINARY
    : ('\'')('b')('0' .. '1')+
    ;

    BOOLEAN
    : '<true,bool>'
    | '<false,bool>'
    ;

//------------------------------------------------------------------------------


//==== Arithmetic Operators ====================================================
PLUS
    : '+'
    ;

MINUS
    : '-'
    ;

TIMES
    : '*'
    ;

DIV
    : '/'
    ;
//------------------------------------------------------------------------------

//==== Relational Operators ====================================================
relop
    : GT
    | GE
    | LT
    | LE
    ;

GT
    : '>'
    ;

GE
    : '>='
    ;

LT
    : '<'
    ;

LE
    : '<='
    ;

EQ
    : '=='
    ;

NEQ
    : '!='
    ;
//------------------------------------------------------------------------------


//==== Logic Operators =========================================================

BAND
    : '&'
    ;

BOR
    : '|'
    ;

BXOR
    : '^'
    ;

NEG
    : '~'
    ;

//------------------------------------------------------------------------------


//==== Boolean Operators ========================================================

AND
    : '&&'
    ;

OR
    : '||'
    ;

NOT
    : '!'
    ;
//------------------------------------------------------------------------------

// Ignore: \r, \n, \t
WS : [ \t\r\n] -> skip;
